apply plugin: 'java'
apply plugin: 'application'

mainClassName = 'br.com.vinyanalista.portugol.ide.Aplicativo'
version = '1.0.3'

dependencies {
	compile project(':interpretador')
	compile files('../lib/autocomplete.jar')
	compile files('../lib/rsyntaxtextarea.jar')
	provided files("$System.env.JAVA_HOME/jre/lib/javaws.jar")
	provided 'com.google.code.findbugs:jsr305:3.0.2'
}

jar {
    dependsOn configurations.runtime
    manifest {
        attributes 'Main-Class': mainClassName
    }
    from {
        (configurations.runtime - configurations.provided).collect {
            it.isDirectory() ? it : zipTree(it)
        }
    }

    include 'br/**'
    include 'org/fife/io/**'
    include 'org/fife/print/**'
    include 'org/fife/ui/autocomplete/**'
    include 'org/fife/ui/rsyntaxtextarea/focusabletip/**'
    include 'org/fife/ui/rsyntaxtextarea/folding/**'
    include 'org/fife/ui/rsyntaxtextarea/parser/**'
    include 'org/fife/ui/rsyntaxtextarea/templates/**'
    include 'org/fife/ui/rsyntaxtextarea/themes/**'

    // Todo esse esforço para excluir do JAR vários arquivos não utilizados
    // dessa pasta:
    include 'org/fife/ui/rsyntaxtextarea/modes/PlainTextTokenMaker.class'
    include 'org/fife/ui/rsyntaxtextarea/modes/WindowsBatchTokenMaker*.class'

    include 'org/fife/ui/rsyntaxtextarea/*.class'
    include 'org/fife/ui/rsyntaxtextarea/*.properties'
    include 'org/fife/ui/rtextarea/**'
    include 'org/fife/util/**'
    include '*.xml'
}

// Assinatura do JAR: requisito para o Java Web Start
// http://www.oracle.com/technetwork/java/javase/tech/java-code-signing-1915323.html

// Antes de compilar, executar:
// $ keytool -genkey -alias portugol -keyalg RSA -keysize 2048 -keystore ~/.keystore.jks

// https://stackoverflow.com/a/42608842/1657502
def keystoreFile = new File(System.getProperty('user.home') + '/.keystore.jks')
def keystorePass = '123456'
def keyAlias = 'portugol'

task signJar(dependsOn: jar) {
    doLast {
        def signdir = new File("$buildDir/libs/signed")
        signdir.mkdirs()
        ant.signjar(
            destDir: "${signdir.absolutePath}",
            jar: 'build/libs/*.jar',
            alias: keyAlias,
            storetype: 'jks',
            keystore: "${keystoreFile.absolutePath}",
            storepass: keystorePass,
            verbose: true,
            preservelastmodified: true
        )
    }
}

build.dependsOn signJar
