apply plugin: 'java'

version = '1.0.3'

dependencies {
	compile project(":interpretador")
	compile files('../lib/autocomplete.jar')
	compile files('../lib/rsyntaxtextarea.jar')
	provided files("$System.env.JAVA_HOME/jre/lib/javaws.jar")
	provided 'com.google.code.findbugs:jsr305:3.0.2'
}

jar {
    dependsOn configurations.runtime
    manifest {
        attributes "Main-Class": "br.com.vinyanalista.portugol.ide.Aplicativo"
    }
    from {
        (configurations.runtime - configurations.provided).collect {
            it.isDirectory() ? it : zipTree(it)
        }
    } {
        exclude "META-INF/*.SF"
        exclude "META-INF/*.DSA"
        exclude "META-INF/*.RSA"
        exclude "org/fife/ui/rsyntaxtextarea/modes/(!{AbstractTokenMaker,PlainTextTokenMaker}).class"
        exclude 'theme.dtd'
    }
}

// Assinatura do JAR: requisito para o Java Web Start
// http://www.oracle.com/technetwork/java/javase/tech/java-code-signing-1915323.html

// Antes de compilar, executar:
// $ keytool -genkey -alias portugol -keyalg RSA -keysize 2048 -keystore ~/.keystore.jks

// https://stackoverflow.com/a/42608842/1657502
def keystoreFile = new File(System.getProperty('user.home') + '/.keystore.jks')
def keystorePass = '123456'
def keyAlias = 'portugol'

task signJar(dependsOn: jar) {
    doLast {
        println ":$project.name:${name}"
        def signdir = new File("$buildDir/libs/signed")
        signdir.mkdirs()
        ant.signjar(
            destDir: "${signdir.absolutePath}",
            jar: 'build/libs/*.jar',
            alias: keyAlias,
            storetype: 'jks',
            keystore: "${keystoreFile.absolutePath}",
            storepass: keystorePass,
            verbose: true,
            preservelastmodified: true
        )
    }
}

build.dependsOn signJar
